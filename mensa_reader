#!/usr/bin/env perl
use Mojolicious::Lite;

use feature 'signatures';
no warnings 'experimental::signatures';

plugin Config => {file => app->home->rel_file('config')};
my $api_base = app->config('api')->{base_url};

# Detect proxy setting from environment
app->ua->proxy->detect;

# Load mensa data including Google Maps link
sub load_mensa_data ($id) {

    # Request and parse JSON
    my $url     = "$api_base/canteens/$id";
    my $data    = app->ua->get($url)->result->json;

    # Build Google Maps URL
    my $gm_url = 'https://www.google.com/maps/place'
        . "/$data->{coordinates}[0],$data->{coordinates}[1]";

    # Done
    return {
        %$data,
        api_url         => $url,
        google_maps_url => $gm_url,
    };
}

# Homepage: redirect to listing
get '/' => sub ($c) {
    $c->redirect_to('list');
} => 'home';

# List all known mensa items for selection
get '/list' => sub ($c) {

    # Retrieve data for each mensa (synchronously, sorted)
    my @mensa_ids   = @{ $c->config('mensa_ids') }; # deref array ref
    my %mensa_data  = map {$_ => load_mensa_data($_)} @mensa_ids;
    my @mensa_data  = sort {$a->{name} cmp $b->{name}} values %mensa_data;

    # Done
    $c->stash(mensa_data => \@mensa_data);

} => 'list';

get '/mensa/:id' => [id => qr/[0-9]+/] => sub ($c) {
    my $mensa_id = $c->param('id');

    # Load mensa base data
    my $mensa_data = load_mensa_data($mensa_id);

    # Load meal data
    my $meal_url    = "$mensa_data->{api_url}/meals";
    my $meal_days   = $c->app->ua->get($meal_url)->result->json;

    # Process days
    for my $day (@$meal_days) {

        # Date: transform 2018-11-23 to 23.11.2018
        $day->{date} = "$3.$2.$1"
            if $day->{date} =~ /^(\d{4})-(\d\d)-(\d\d)$/;
    }

    # Done
    $c->stash(
        mensa_data  => $mensa_data,
        meal_days   => $meal_days,
    );

} => 'mensa';

app->start;
__DATA__

@@ list.html.ep
% layout 'default';
% title 'Welcome';
<h1>Yay! Mensa Reader!</h1>
<p>All known mensas:</p>
<ul id="mensas">
% for my $d (@$mensa_data) {
    <li>
        %= link_to mensa => {id => $d->{id}} => begin
            <strong><%= $d->{name} %></strong>
        % end
        <br>
        %= link_to "ðŸ“Œ $d->{address}" => $d->{google_maps_url}
    </li>
% }
</ul>

@@ mensa.html.ep
% layout 'default';
% title $mensa_data->{name};
<p><%= link_to 'Mensa-Ãœbersicht' => 'list' %></p>
<h1><%= $mensa_data->{name} %></h1>
% for my $day (@$meal_days) {
    <h2><%= $day->{date} %></h2>
    %= include 'meal_day_details', day => $day
% }
<p><%= link_to 'Mensa-Ãœbersicht' => 'list' %></p>

@@ meal_day_details.html.ep
% unless ($day->{closed}) {
    <table>
        <thead>
            <tr>
                <th colspan="2">Gericht</th>
                <th>Preis (â‚¬)</th>
            </tr>
        </thead>
        <tbody>
        % for my $meal (@{$day->{meals}}) {
            <tr>
                <th><%= $meal->{category} %></th>
                <td>
                    %= $meal->{name}
                    <ul class="notes">
                    % for my $note (@{$meal->{notes}}) {
                        <li><%= $note %></li>
                    % }
                    </ul>
                </td>
                % my @roles  = qw(students employees others);
                % my @prices = map $meal->{prices}{$_} => @roles;
                % my @euros  = map sprintf('%.2f â‚¬' => $_) => @prices;
                <td title="<%= join ' / ' => @roles %>">
                    <span class="price"><%= join ' / ' => @euros %></span>
                </td>
            </tr>
        % }
        </tbody>
    </table>
% } else {
    <p>Geschlossen</p>
% }

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
    <head>
        <title>MensaReader: <%= title %></title>
        %= stylesheet begin
            body { font-family: sans-serif; margin: 5em 8em }
            a:link { text-decoration: none }
            a:hover { background: #f8f8f8 }
            ul#mensas li { margin-bottom: .5em }
            table { border-collapse: collapse }
            th, td { padding: .8em 1.2em }
            tbody tr > * { border-top: thin solid black }
            tr td { border-left: thin solid lightgrey }
            table:hover th, table:hover td { background-color: #f8f8f8 }
            .notes { font-size: .75em; color: #666; line-height: 1.2em }
            .price { white-space: pre }
        % end
    </head>
    <body>
        %= content
    </body>
</html>
